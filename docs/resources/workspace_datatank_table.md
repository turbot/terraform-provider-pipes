---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "pipes_workspace_datatank_table Resource - terraform-provider-pipes"
subcategory: ""
description: |-
  The `Turbot Pipes Workspace Datatank Table` represents a table configured for a datatank in a workspace.
---

# Resource: pipes_workspace_datatank_table

Manages a workspace datatank table.

## Example Usage

**Create a user workspace datatank table with source schema as an aggregator**

```hcl
resource "pipes_workspace" "test_user_workspace" {
  handle = "dev"
}

resource "pipes_connection" "test_connection_net_1" {
  handle     = "net_1"
  plugin     = "net"
}

resource "pipes_connection" "test_connection_net_2" {
  handle     = "net_2"
  plugin     = "net"
}

resource "pipes_workspace_connection" "test_connection_net_1_association" {
  workspace_handle  = pipes_workspace.test_user_workspace.handle
  connection_handle = pipes_connection.test_connection_net_1.handle
}

resource "pipes_workspace_connection" "test_connection_net_2_association" {
  workspace_handle  = pipes_workspace.test_user_workspace.handle
  connection_handle = pipes_connection.test_connection_net_2.handle
}

resource "pipes_workspace_aggregator" "test_aggregator_all_net" {
  workspace          = pipes_workspace.test_user_workspace.handle
  handle             = "all_net"
  plugin             = "net"
  connections        = ["*"]
}

resource "pipes_workspace_datatank" "test_datatank_fast_net" {
  workspace_handle   = pipes_workspace.test_user_workspace.handle
  handle 		     = "fast_net"
}

resource "pipes_workspace_datatank_table" "test_datatank_table_net_certificate" {
  workspace_handle = pipes_workspace.test_user_workspace.handle
  datatank_handle  = pipes_workspace_datatank.test_datatank_fast_net.handle
  name 		       = "net_certificate"
  type             = "table"
  part_per         = "connection"
  source_schema	   = "all_net"
  source_table	   = "net_certificate"
  frequency        = jsonencode({
    "type": "interval",
    "schedule": "daily"
  })

  depends_on       = [pipes_workspace_aggregator.test_aggregator_all_net]
}
```

**Create an organization workspace datatank table with source schema as an aggregator**

```hcl
resource "pipes_workspace" "test_org_workspace" {
  organization = "testorg"
  handle       = "dev"
}

resource "pipes_connection" "test_connection_net_1" {
  organization = pipes_workspace.test_org_workspace.organization
  handle       = "net_1"
  plugin       = "net"
}

resource "pipes_connection" "test_connection_net_2" {
  organization = pipes_workspace.test_org_workspace.organization
  handle       = "net_2"
  plugin       = "net"
}

resource "pipes_workspace_connection" "test_connection_net_1_association" {
  organization      = pipes_workspace.test_org_workspace.organization
  workspace_handle  = pipes_workspace.test_org_workspace.handle
  connection_handle = pipes_connection.test_connection_net_1.handle
}

resource "pipes_workspace_connection" "test_connection_net_2_association" {
  organization      = pipes_workspace.test_org_workspace.organization
  workspace_handle  = pipes_workspace.test_org_workspace.handle
  connection_handle = pipes_connection.test_connection_net_2.handle
}

resource "pipes_workspace_aggregator" "test_aggregator_all_net" {
  organization       = pipes_workspace.test_org_workspace.organization
  workspace          = pipes_workspace.test_org_workspace.handle
  handle             = "all_net"
  plugin             = "net"
  connections        = ["*"]
}

resource "pipes_workspace_datatank" "test_datatank_fast_net" {
  organization      = pipes_workspace.test_org_workspace.organization
  workspace_handle  = pipes_workspace.test_org_workspace.handle
  handle 		    = "fast_net"
}

resource "pipes_workspace_datatank_table" "test_datatank_table_net_certificate" {
  organization     = pipes_workspace.test_org_workspace.organization
  workspace_handle = pipes_workspace.test_org_workspace.handle
  datatank_handle  = pipes_workspace_datatank.test_datatank_fast_net.handle
  name 		       = "net_certificate"
  type             = "table"
  part_per         = "connection"
  source_schema	   = "all_net"
  source_table	   = "net_certificate"
  frequency        = jsonencode({
    "type": "interval",
    "schedule": "daily"
  })

  depends_on       = [pipes_workspace_aggregator.test_aggregator_all_net]
}
```

## Argument Reference

The following arguments are supported:

- `datatank_handle` - (Required) The handle of the datatank where the table will be managed.
- `frequency` - (Required) The frequency at which data in the table will be refreshed.
- `name` - (Required) A friendly name for the table which will be used to persist data.
- `type` - (Required) The type of the table. Valid values are `table` and `query`.
- `workspace_handle` - (Required) The handle of the workspace to manage the datatank for.
- `description` - (Optional) A description for the table.
- `desired_state` - (Optional) The desired state of the datatank table, which can be set only after it has already been created. Valid values are `enabled`, `disabled` and `paused`.
- `organization` - (Optional) The optional handle of the organization to be used when the datatank table to be managed belongs to an organization.
- `part_per` - (Optional) The partitioning strategy for the table i.e. creates a separate partition for each connection in `source_schema`. Only valid value is `connection`.
- `source_query` - (Optional) The query to be used when refreshing data for the table. Required when `type = query`.
- `source_schema` - (Optional) The handle of the schema to be used when refreshing data for the table. Required when `type = table` and/or `part_per = connection`.
- `source_table` - (Optional) The handle of the table to be used when refreshing data for the table. Required when `type = table`.

## Attributes Reference

In addition to all arguments above, the following attributes are exported:

- `created_at` - The ISO 8601 date & time the datatank was created at.
- `created_by` - The unique identifier of the actor that created this datatank.
- `datatank_id` - The unique identifier of the datatank.
- `datatank_table_id` - The unique identifier of the datatank table.
- `freshness` - A summary of the freshness of the datatank table and its parts.
- `migrating_freshness` - A summary of the freshness of the datatank table and its parts when the table is migrating.
- `migrating_name` - The name of the datatank table when the table is migrating.
- `state` - The current state of the datatank table.
- `state_reason` - The reason for the current state of the datatank table.
- `updated_at` - The ISO 8601 date & time the datatank was last updated at.
- `updated_by` - The unique identifier of the actor that last updated this datatank.
- `version_id` - The version ID of the datatank.

## Import

### Import User Workspace Datatank Table

User workspace datatank tables can be imported using an ID made up of `workspace_handle/datatank_handle/datatank_table_name`, e.g.,

```sh
terraform import pipes_workspace_datatank_table.example dev/fast_net/net_certificate
```

### Import Organization Workspace Datatank Table

Organization workspace datatank tables can be imported using an ID made up of `organization/workspace_handle/datatank_handle/datatank_table_name`, e.g.,

```sh
terraform import pipes_workspace_datatank_table.example myorg/dev/fast_net/net_certificate
```
