---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "pipes_tenant_connection Resource - terraform-provider-pipes"
subcategory: ""
description: |-
  Provides a Pipes Connection resource on a tenant. The `Turbot Pipes Connection` represents a set of tables for a single data source. Each connection is represented as a distinct Postgres schema. In order to query data, you'll need at least one connection.

  Connections can be defined at the tenant, organization or workspace level.
---

# Resource: pipes_tenant_connection

Manages a connection, which is defined on a tenant.

## Example Usage

**Create an AWS connection**

```hcl
resource "pipes_tenant_connection" "aws_aaa" {
  plugin = "aws"
  handle = "aws_aaa"
  config = jsonencode({
    access_key = "redacted"
    secret_key = "redacted"
    regions    = ["us-east-1"]
  })
}
```

**Create an AWS connection using IAM role mode**

This example requires the AWS provider, but if you have an existing role, you
can just create the `pipes_tenant_connection` resource.

```hcl
data "pipes_user" "caller" {}

resource "random_string" "random" {
  length  = 8
  special = false
  upper   = false
}

locals {
  # The format of external id is ^(u|o)_[a-z0-9_]{20}:[a-z0-9]{8}$`
  external_id = "${data.pipes_user.caller.user_id}:${random_string.random.id}"
}

provider "aws" {
  region = "us-east-1"
}

# Create AWS IAM role
resource "aws_iam_role" "pipes_role" {
  name = "pipes"
  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Sid    = ""
        Principal = {
          AWS = "arn:aws:iam::123456789012:root"
        },
        Condition = {
          StringEquals = {
            "sts:ExternalId" = local.external_id
          }
        }
      },
    ]
  })
}

data "aws_iam_policy" "ReadOnlyAccess" {
  arn = "arn:aws:iam::aws:policy/ReadOnlyAccess"
}

# Attach ReadOnlyAccess policy to role
resource "aws_iam_role_policy_attachment" "pipes_role_attach" {
  role       = aws_iam_role.pipes_role.name
  policy_arn = data.aws_iam_policy.ReadOnlyAccess.arn
}

# Create connection with IAM role
resource "pipes_tenant_connection" "aws_role_connection" {
  handle      = "aws_role_connection"
  plugin      = "aws"
  config = jsonencode({
    regions     = ["us-east-1", "us-east-2"]
    role_arn    = aws_iam_role.pipes_role.arn
    external_id = local.external_id
  })
}
```

**Create a GCP connection**

```hcl
resource "pipes_tenant_connection" "gcp_aaa" {
  handle      = "gcp_aaa"
  plugin      = "gcp"
  config = jsonencode({
    project     = "project-aaa"
    credentials = file("/Users/myuser/Downloads/project-aaa.json")
  })
}
```

**Create a GCP connection within connection folder**

```hcl
resource "pipes_tenant_connection" "oci_aaa" {
  handle       = "oci"
  plugin       = "oci"
  parent_id    = "c_cqlp0647sic7l5q2n5d0"
  config = jsonencode({
    user_ocid    = "ocid1.user.oc1..aaaaaaaaw..."
    fingerprint  = "f1:fc:44:3a:..."
    tenancy_ocid = "ocid1.tenancy.oc1..aaaaaaaah..."
    regions      = ["ap-mumbai-1", "us-ashburn-1"]
    private_key  = file("/Users/myuser/Downloads/mykey.cer")
  })
}
```

## Argument Reference

The following arguments are supported:

- `handle` - (Required) A friendly identifier for your connection, and must be unique across your connections.
- `plugin` - (Required) The name of the plugin.
- `config` - (Optional) Configuration for the connection.
- `parent_id` - (Optional) Identifier of the connection folder in which the connection will be created. If nothing is passed the connection is created at the root level of the tenant.

For each connection resource, additional arguments are supported based on the plugin it uses. For instance, if creating a connection that uses the Zendesk plugin, the [Zendesk configuration arguments](https://hub.steampipe.io/plugins/turbot/zendesk#configuration) should be used in the connection:

```hcl
resource "pipes_tenant_connection" "zendesk" {
  plugin    = "zendesk"
  handle    = "zendesk_example"
  config = jsonencode({
    subdomain = "dmi"
    email     = "pam@dmi.com"
    token     = "17ImlCYdfZ3WJIrGk96gCpJn1fi1pLwexample"
  })
}
```

We do not recommend storing sensitive or secret information in such a way that it's accidentally exposed or can be accessed by unwanted actors. For more information on how to protect sensitive information, please see [Protect Sensitive Input Variables](https://learn.hashicorp.com/tutorials/terraform/sensitive-variables).

## Attributes Reference

In addition to all arguments above, the following attributes are exported:

- `config_source` - Source of the public configuration for the connection. Possible values are `self` or `inherited`. If `inherited`, the connection uses the configuration from the integration that is managing it.
- `connection_id` - Unique identifier of the connection.
- `created_at` - The time when the connection was created.
- `created_by` - The handle of the user who created the connection.
- `credential_source` - Source of the secret credentials for the connection. Possible values are `self` or `inherited`. If `inherited`, the connection uses the credentials from the integration that is managing it.
- `handle_dynamic` - The dynamic handle assigned to the connection when it is discovered by an integration.
- `handle_mode` - Mode of the handle for the connection. Possible values are `static` or `dynamic`. If `dynamic`, the handle is assigned when the connection is discovered by an integration and can be overridden by the user.
- `integration_resource_identifier` - Unique identifier of the resource if the connection is managed by an integration.
- `integration_resource_name` - Name of the resource if the connection is managed by an integration.
- `integration_resource_path` - Path of the resource in the hierarchy if the connection is managed by an integration.
- `integration_resource_type` - Type of the resource if the connection is managed by an integration. For example, if the connection is managed by an AWS integration, the value will be `aws_account`.
- `managed_by_id` - Unique identifier of the integration that is managing the connection, if any.
- `plugin_version` - The version of the plugin being used by the connection.
- `tenant_id` - Unique identifier of the tenant where the connection exists.
- `trunk` - An array of items that represent the path of the connection in the hierarchy, ordered from ancestor to parent.
- `type` - Denotes whether the connection is discovered or manually created. Possible values are `connection` or `connection-discovered`.
- `updated_at` - The time when the connection was last updated.
- `updated_by` - The handle of the user who last updated the connection.
- `version_id` - The connection version.

## Import

Tenant connections can be imported using an ID made up of `tenant_handle/connection_handle`, e.g.,

```sh
terraform import pipes_tenant_connection.example acme/aws_aaa
```
